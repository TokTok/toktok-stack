load(":build_defs.bzl", "WORKFLOWS")

exports_files(srcs = [
    "cabal_test",
    "diff_test.sh",
    "haskell/cirrus.yml.in",
    "haskell/github/docker/Dockerfile.in",
    "mkstamp_c.sh",
    "readme_test.sh",
    "settings_test.sh",
    "yamllint.rc",
    "yamllint_test.sh",
] + [
    "%s/%s" % (lang, wf)
    for lang, wfs in WORKFLOWS.items()
    for wf in wfs
])

# Test that language-specific workflows are super-sets of common workflows.
[sh_test(
    name = "%s_github_%s_test" % (lang, wf),
    size = "small",
    srcs = ["//hs-github-tools/tools:check-workflows"],
    args = [
        "workflows",
        "$(location common/github/workflows/%s.base)" % wf,
        "$(location %s/github/workflows/%s)" % (lang, wf),
    ],
    data = [
        "common/github/workflows/%s.base" % wf,
        "%s/github/workflows/%s" % (lang, wf),
    ],
    tags = ["haskell"],
) for lang in WORKFLOWS.keys() for wf in [
    wf[wf.rfind("/") + 1:wf.rfind(".")]
    for wf in glob(
        ["%s/github/workflows/*.yml" % lang],
        allow_empty = True,
    )
    if glob(
        ["common/github/workflows/%s.base" % wf[wf.rfind("/") + 1:wf.rfind(".")]],
        allow_empty = True,
    )
]]
